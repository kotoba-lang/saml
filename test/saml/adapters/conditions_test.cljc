(ns saml.adapters.conditions-test
  (:require [clojure.test :refer [deftest is]]
            [saml.adapters.conditions :as conditions]
            [saml.adapters.xml :as xml]
            [saml.core :as c]
            [saml.model :as m]))

(defn- delegate [claims]
  (reify xml/IXmlSignatureVerifier
    (verify-saml! [_ _ _] claims)))

(deftest accepts-assertion-inside-condition-window
  (let [verifier (conditions/condition-verifier
                  (delegate {:issuer "https://idp.example"
                             :subject "alice"
                             :audience "sp"
                             :not-before "2026-07-01T00:00:00Z"
                             :not-on-or-after "2026-07-01T00:05:00Z"})
                  {:now "2026-07-01T00:03:00Z"})
        port (xml/verifier-port verifier {})
        req (m/authn-request "s1" {:issuer "sp" :acs-url "https://sp.example/acs"})]
    (is (:saml.assertion/ok? (c/verify port req "kagi://saml/assertion")))))

(deftest rejects-assertion-outside-condition-window
  (let [verifier (conditions/condition-verifier
                  (delegate {:issuer "https://idp.example"
                             :subject "alice"
                             :audience "sp"
                             :not-before "2026-07-01T00:00:00Z"
                             :not-on-or-after "2026-07-01T00:05:00Z"})
                  {:now "2026-07-01T00:05:00Z"})
        port (xml/verifier-port verifier {})
        req (m/authn-request "s2" {:issuer "sp" :acs-url "https://sp.example/acs"})]
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs ExceptionInfo)
                 (c/verify port req "kagi://saml/assertion")))))

#?(:clj
   (deftest accepts-small-clock-skew-around-condition-window
     (let [verifier (conditions/condition-verifier
                     (delegate {:issuer "https://idp.example"
                                :subject "alice"
                                :audience "sp"
                                :not-before "2026-07-01T00:00:10Z"
                                :not-on-or-after "2026-07-01T00:05:00Z"})
                     {:now "2026-07-01T00:00:05Z"
                      :clock-skew-seconds 10})
           port (xml/verifier-port verifier {})
           req (m/authn-request "s3" {:issuer "sp" :acs-url "https://sp.example/acs"})]
       (is (:saml.assertion/ok? (c/verify port req "kagi://saml/assertion"))))))
